## slm-run  --config config.bok  -f queries/bok-graph.sparql --debug
PREFIX ns1: <http://example.org/course/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ex: <http://example.org/>
PREFIX align: <http://align.org/>

SELECT DISTINCT ?course ?label ?ku_source ?score ?track ?ka ?ku ?answer ?explain 
WHERE {
    BIND(ex:SLM-FILE("./data/courses-anon.ttl") AS ?input)
    BIND(ex:SLM-RDF(?input,"turtle") AS ?ginput)
    graph ?ginput { 
        { select * where {
            ?course ns1:content ?content .
            ?course rdfs:label ?label .
            ?course ns1:objective ?objective .
            ?course ns1:parcours ?track .
        } limit 1 offset {OFFSET}
#        } limit 2 
       }
    }
    BIND(CONCAT("Label: ",STR(?label)," Objectif: ",STR(?objective),
                " Course content: ", STR(?content)," Course name: ",STR(?course)) AS ?course_desc)
    BIND(ex:SLM-SEARCH-FAISS(?course_desc,?course,3) AS ?search_graph)
    GRAPH ?search_graph {
        ?course ex:is_aligned_with ?bn .
        ?bn ex:has_score ?score .
        ?bn ex:has_source ?ku_source .
        ?bn ex:has_chunk ?chunk .
    }
    BIND (REPLACE(str(?ku_source), "file://.*/([^/]+)/[^/]+\\.txt$", "$1") AS ?ka) 
    BIND (REPLACE(str(?ku_source), "file://.*/([^/]+)/([^/]+)\\.txt$", "$2") AS ?ku)
    BIND(ex:SLM-READFILE(?ku_source) AS ?ku_content)

    BIND(CONCAT("""
        This first page describes the content of a lecture,
        the second page describes the content of a Knowledge Unit in Computer Science:
        <page1>""",STR(?course_desc), " </page1> <page2> ",STR(?ku_content),
        """</page2> 
        Does the lecture cover a large subset of the knowledge Unit. Answer in JSN-LD only with a type  Report",
                "@context": "https://schema.org/",
                "@type": "Report",
                "http://schema.org/answer": "A",
                "http://schema.org/explain": "B",
        Replace A by 0 it does not cover, 1 if it cover.
        Replace B by a very short explanation""") AS ?prompt)
    BIND(ex:SLM-LLMGRAPH(?prompt,?course) AS ?llm_graph)
    GRAPH ?llm_graph {
        ?course ex:has_schema_type ?root .
        ?root a <http://schema.org/Report>  .
        ?root <http://schema.org/answer> ?answer .
        ?root <http://schema.org/explain> ?explain .    
    }
} 